'****************************************************************
'* Jake's Amazing Toolbar - Main                                *
'* 2009-2011 All Rights Reserved                                *
'* Source available at github.com/mrchimp/JakesMapbasicToolbars *
'****************************************************************

Include "mapbasic.def"
Include "icons.def"
Include "globals.def"

Declare Sub Main
Declare Sub GTFO
Declare Sub Bufferiser
Declare Sub JakesToolsHelp
Declare Sub CreateDir

Sub Main                                                            'Blockstart
  OnError goto MainError

  SettingsURI = GetFolderPath$(FOLDER_MI_APPDATA) & "JakesMITools.txt"
  Call CheckSettings
  
  ' Define some globals
  AreaUnitsArray(1)  = "hectare"
  AreaUnitsArray(2)  = "acre"
  AreaUnitsArray(3)  = "perch"
  AreaUnitsArray(4)  = "rood"
  AreaUnitsArray(5)  = "sq ch"
  AreaUnitsArray(6)  = "sq cm"
  AreaUnitsArray(7)  = "sq ft"
  AreaUnitsArray(8)  = "sq in"
  AreaUnitsArray(9)  = "sq km"
  AreaUnitsArray(10) = "sq li"
  AreaUnitsArray(11) = "sq m"
  AreaUnitsArray(12) = "sq mi"
  AreaUnitsArray(13) = "sq mm"
  AreaUnitsArray(14) = "sq rd"
  AreaUnitsArray(15) = "sq survey ft"
  AreaUnitsArray(16) = "yd"
  
  ' NOTE: These two arrays are equivalent. 
  ' If you change one, CHANGE THE OTHER!!
  DistUnits(1)  = "m"
  DistUnits(2)  = "km"
  DistUnits(3)  = "cm"
  DistUnits(4)  = "mm"
  DistUnits(5)  = "mi"
  DistUnits(6)  = "nmi"
  DistUnits(7)  = "rd"
  DistUnits(8)  = "ft"
  DistUnits(9)  = "survey ft"
  DistUnits(10) = "yd"
  DistUnits(11) = "li"
  DistUnits(12) = "ch"
  DistUnits(13) = "in"
  DistUnitNames(1)  = "Meters"
  DistUnitNames(2)  = "Kilometers"
  DistUnitNames(3)  = "Centimeters"
  DistUnitNames(4)  = "Millimeters"
  DistUnitNames(5)  = "Miles"
  DistUnitNames(6)  = "Nautical Miles"
  DistUnitNames(7)  = "Rods"
  DistUnitNames(8)  = "Feet (international)"
  DistUnitNames(9)  = "US Survey Feet"
  DistUnitNames(10) = "yards"
  DistUnitNames(11) = "Links"
  DistUnitNames(12) = "Chains"
  DistUnitNames(13) = "Inches"
  
  ' Image types available to the Save Window command.
  FileTypeArray(1)  = "PNG"
  FileTypeArray(2)  = "BMP"
  FileTypeArray(3)  = "WMF"
  FileTypeArray(4)  = "JPEG"
  FileTypeArray(5)  = "JP2"
  FileTypeArray(6)  = "TIFF"
  FileTypeArray(7)  = "TIFFCMYK"
  FileTypeArray(8)  = "TIFFG4"
  FileTypeArray(9)  = "TIFFLZW"
  FileTypeArray(10) = "GIF"
  FileTypeArray(11) = "PSD"
  FileTypeArray(12) = "EMF"
  FileTypeArray(13) = "GEOTIFF"

  ' Formats used by MultiExport
  ' NOTE: The OGR part is now a misnomer.
  OGRFormats(1)  = "ASCII"
  OGRFormats(2)  = "BNA"
  OGRFormats(3)  = "CSV"
  OGRFormats(4)  = "DBF"
  OGRFormats(5)  = "DGN"
  OGRFormats(6)  = "DXF"
  OGRFormats(7)  = "ESRI Shapefile"
  OGRFormats(8)  = "GeoJSON"
  OGRFormats(9)  = "Geoconcept"
  OGRFormats(10)  = "GeoRSS"
  OGRFormats(11) = "GML"
  OGRFormats(12) = "GMT"
  OGRFormats(13) = "GPX"
  OGRFormats(14) = "GPSTrackMaker"
  OGRFormats(15) = "KML"
  OGRFormats(16) = "Mapinfo File"
  OGRFormats(17) = "Memory"
  OGRFormats(18) = "MIF"
  
  ' Make some default styles
  PenStyle   = MakePen(6,2,RED)
  BrushStyle = MakeBrush(01,RED,RED)
  PointStyle = MakeSymbol(34,RED,18)
  
  ' Load menu
  Create Menu "WindowTools" as
    "Create A4 &Landscape Layout from Mapper" 
      HelpMsg "Creates a new A4 Layout window using the currently selected window."
      Calling NewA4LayoutL,
    "Create A4 &Portait Layout from Mapper"
      HelpMsg "Creates a new A4 Layout window using the currently selected window."
      Calling NewA4LayoutP,
    "Create A3 L&andscape Layout from Mapper"
      HelpMsg "Creates a new A3 Layout window using the currently selected window."
      Calling NewA3LayoutL,
    "Create A3 P&ortrait Layout from Mapper"
      HelpMsg "Creates a new A3 Layout window using the currently selected window."
      Calling NewA3LayoutP,
    "(-",
    "&Rename Window" 
      HelpMsg "Changes the title of the current window."
      Calling RenameWindow,
    "&Duplicate Window" 
      HelpMsg "Duplicates the currently selected window."
      Calling DuplicateWin,
    "Rotate Layout Window (beta)"
      HelpMsg "Makes a portrait layout window landscape and vice versa. Doesn't affect window content or non-layout windows."
      Calling RotateWin,
    "View Window &Info"
      HelpMsg "Returns as much info as possible about the front-most window."
      Calling GetWindowInfo
  
  Create Menu "Export" As
    "&Site Plans..."
      HelpMsg "Exports one image per record for a given layer."
      Calling SitePlanExporter,
    "&MIF DXF and SHP..."
      HelpMsg "Export a MIF, DXF and SHP version of a given table."
      Calling MifDxfShpExporter,
    "&Batch Table Exporter (beta)..."
      HelpMsg "Exports multiple files to multiple formats."
      Calling MultiExport

  Create Menu "ViewTool" As
    "&Save View"
      HelpMsg "Save the location and zoom setting of the current map window."
      Calling addView,
    "&Go To View"
      HelpMsg "Move the current map window to a location stored with the above save command."
      Calling showView,
    "(-",
    "Show &Table"
      HelpMsg "View the table in which views are stored."
      Calling ShowViewTable
  
  Create Menu "&CoordsysTools" As
    "View &Table Coord System..."
      HelpMsg "Select a table from a list and view it's coordinate system descriptor."
      Calling GetTableCoordSys,
    "View &Window Coord System..." 
      HelpMsg "View the coordinate system descriptor of the selected window."
      Calling GetWindowCoordSys
  
  Create Menu "&Overlaps" As
    "&Single Table..."
      HelpMsg "Show which records overlap other records from the same table."
      Calling OverlapDetector,
    "&Single Table -> Multiple Tables..."
      HelpMsg "Find overlaps between one table and multiple other tables."
      Calling CheckConstraints
  
  Create Menu "&Jake's Tools" As
    "&Coordsys" as "&CoordsysTools",
    "&Export"   as "Export",
    "&Overlaps" as "Overlaps",
    "&Views"    as "ViewTool",
    "&Window"   as "WindowTools",
    "(-",
    "&Unmapped Records..." 
      HelpMsg "Display all records froma given table which do not have an object attached."
      Calling ShowUnmapped,
    "&Area Update..."
      HelpMsg "Update the area column of a table byt calculating the areas of the attached objects."
      Calling SiteAreaUpdate,
    "(-",
    "Settings"
      HelpMsg "View and change settings."
      Calling SettingsDialog,
    "&Help" 
      HelpMsg "This is a help message about a help message. Click help to get help. I can't make it any easier."
      Calling JakesToolsHelp,
    "C&lose"
      Helpmsg "Close Jake's Toolbar"
      Calling GTFO

  Alter Menu Bar Add "&Jake's Tools"
  
  Create ButtonPad "Jake's Other Tools" As 
    Separator
    ToolButton
      Icon MI_ICON_LINE
      HelpMsg "Click and drag on a map window to draw a line."
      Cursor MI_CURSOR_CROSSHAIR
      DrawMode DM_CUSTOM_LINE
      Calling LineTool
    ToolButton
      Icon MI_ICON_ARROW_19
      HelpMsg "Click on map to draw circle."
      Cursor MI_CURSOR_CROSSHAIR
      DrawMode DM_CUSTOM_POINT
      Calling CircleTool
    Show
  
  Exit Sub
	
MainError:
    Note "Error: "+ Error$()
End Sub                                                             'Blockend

Sub GTFO                                                            'Blockstart
  Dim ImDoneHere as Logical
  ImDoneHere = Ask("Are you sure?", "&Yes", "&No")
  If ImDoneHere Then
    End Program
  End If
End Sub                                                             'Blockend

Sub JakesToolsHelp                                                  'Blockstart
  Note "Jake's MapBasic Tools" & Chr$(13) & 
       "by Jake Gully" & Chr$(13)  & Chr$(13) & 
       "Version " & TOOLBAR_VERSION
End Sub                                                             'Blockend
