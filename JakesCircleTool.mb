'****************************************************
'* Jake's Amazing Circle Tool                       *
'* https://github.com/mrchimp/JakesMapbasicToolbars *
'****************************************************

Include "mapbasic.def"
Include "icons.def"

Declare Sub Main
Declare Sub CircleTool
Declare Sub UpdateValues
Declare Sub GetEditableTable(TableName as String)
Declare Sub ExitProg

Dim InputTypes(3) as String
Dim UnitTypes(3) as String

Define pi 3.1415926535897932384626433832795
Define INPUT_AREA 1
Define INPUT_CIRCUMFERENCE 2
Define INPUT_RADIUS 3

Sub Main
  'Set Distance Units "m"
  'Set CoordSys Window FrontWindow()

  InputTypes(1) = "Area"
  InputTypes(2) = "Circumference"
  InputTypes(3) = "Radius"

  UnitTypes(1) = "m / sq m"
  UnitTypes(2) = "cm / sq cm"
  UnitTypes(3) = "miles / sq miles"

  Alter ButtonPad "Drawing"
    Add 
    Separator
    ToolButton
    Icon MI_ICON_ARROW_19
    HelpMsg "Click on map to draw circle."
    Cursor MI_CURSOR_CROSSHAIR
    DrawMode DM_CUSTOM_POINT
    Calling CircleTool
    Show

  Create Menu "Jake's Tools" As
    "Close Jake's Circle Tool" Calling ExitProg

  Alter menu "Tools" Add
    "Jake's Tools" as "Jake's Tools"
End Sub

Sub ExitProg
  End Program
End Sub

Sub GetEditableTable(TableName as String)
  Dim i as integer
  Dim j as integer
  Dim TableList(1) as String
  i = NumTables()
  ReDim TableList(i)
  For j = 1 to i
    If TableInfo(j, LAYER_INFO_EDITABLE) Then
      TableName = TableInfo(j, TAB_INFO_NAME)
      Exit Sub
    End If
  Next
End Sub

Sub CircleTool
  OnError Goto TheSectionOfFail

  Set Distance Units "m" 
  Set CoordSys Window FrontWindow()

  Dim InputVal, a, r, c, d, x, y As float
  Dim currentTab As String
  Dim InputType  As Integer
  Dim NiceCircle As Object

  x = CommandInfo(CMD_INFO_X)
  y = CommandInfo(CMD_INFO_Y)

  Dialog
    Title "Circle Tool"
    
    Control PopupMenu
      Title From Variable InputTypes
      Into  InputType
    Control StaticText
      Title "="
    Control EditText
      Into  InputVal
    Control StaticText
      Title "m" 
    
    Control OkButton
      Title "Create Circle"
    Control CancelButton
      Title "Close"

  If CommandInfo(CMD_INFO_DLG_OK) Then

    If InputVal <= 0 Then
      Note "Bigger number please."
      Exit Sub
    End If
  
    Do Case InputType
      Case INPUT_AREA
        a = InputVal
        r = Sqr(a / pi)
        c = r * 2 * pi
      Case INPUT_CIRCUMFERENCE
        c = InputVal
        r = c / (2 * pi)
        a = pi * r * r
      Case INPUT_RADIUS
        r = InputVal
        c = 2 * pi * r
        a = pi * r * r
    End Case
  
    d = r * 2

    currentTab = windowInfo(FrontWindow(), WIN_INFO_TABLE)
    
    NiceCircle = CreateCircle(x, y, r)
    INSERT INTO currentTab (obj) VALUES (NiceCircle)
  End If
     
  Exit Sub
	
TheSectionOfFail:
  Note "Error: "+ Error$()
End Sub

